# ~/.zshrc

# Enable profiling only when requested
if [[ -n "$ZSH_PROFILE" ]]; then
  zmodload zsh/zprof
fi

# XDG
export XDG_CONFIG_HOME="$HOME/.config"
export XDG_CACHE_HOME="$HOME/.cache"
export ZSH_CONFIG="$XDG_CONFIG_HOME/zsh"
export ZSH_CACHE="$XDG_CACHE_HOME/zsh"

# Editor & pager
export VISUAL=vim
export EDITOR=$VISUAL

# Locale (optional but handy)
export LC_ALL=en_US.UTF-8
export LANG=en_US.UTF-8

# Colors configuration
if command -v dircolors >/dev/null 2>&1; then
  # Use GNU dircolors + LS_COLORS
  [[ -r "$XDG_CONFIG_HOME/dircolors" ]] && eval "$(dircolors -b "$XDG_CONFIG_HOME/dircolors")"
else
  # Fallback for macOS BSD ls: define LSCOLORS instead
  # Example: ExGxFxDxCxDxDxBxBxBx
  # Each pair = fg/bg code for file types. See "man ls" on macOS.
  export LSCOLORS="ExGxFxDxCxDxDxBxBxBx"
fi

# GREP_COLORS works only with GNU grep (Homebrew)
[[ -r "$XDG_CONFIG_HOME/grepcolors.sh" ]] && source "$XDG_CONFIG_HOME/grepcolors.sh"

# PATH: unique & ordered (Zsh auto-syncs PATH<->path)
typeset -U path   # make entries unique; keep leftmost

# User bins first (prepend)
[[ -d "$HOME/.local/bin" ]]            && path=("$HOME/.local/bin" $path)
[[ -d "$HOME/projects/private/bin" ]]  && path=("$HOME/projects/private/bin" $path)
# [[ -d "$HOME/Library/Python/3.11/bin" ]] && path=("$HOME/Library/Python/3.11/bin" $path)

# Homebrew & GNU tools
if [[ -x "/opt/homebrew/bin/brew" ]]; then
  eval "$(/opt/homebrew/bin/brew shellenv)"
  export HOMEBREW_NO_EMOJI=1

  # Insert GNU shims in a stable order (prepend)
  for d in \
    /opt/homebrew/opt/coreutils/libexec/gnubin \
    /opt/homebrew/opt/findutils/libexec/gnubin \
    /opt/homebrew/opt/gnu-sed/libexec/gnubin \
    /opt/homebrew/opt/grep/libexec/gnubin \
    /opt/homebrew/opt/curl/bin
  do
    [[ -d "$d" ]] && path=("$d" $path)
  done
fi

# Source additional ZSH configuration (quietly)
for file in "$ZSH_CONFIG"/{options,completion,prompt,aliases,plugins}.zsh; do
  [[ -r "$file" ]] && source "$file"
done

# Local overrides
[[ -r "$ZSH_CONFIG/local.zsh" ]] && source "$ZSH_CONFIG/local.zsh"
